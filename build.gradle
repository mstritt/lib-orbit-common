/*
 *     Orbit, a versatile image analysis software for biological image-based quantification.
 *     Copyright (C) 2009 - 2017 Idorsia Pharmaceuticals Ltd., Hegenheimermattweg 91, CH-4123 Allschwil, Switzerland.
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

buildscript {
    repositories {
        maven {
            url 'http://nexus.idorsia.com/repository/maven-central/'
            content {
                excludeGroup "com.idorsia.research"
                excludeGroup "com.actelion.research"
            }
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.22.0"
    }
}

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'

//ext.libOrbitCommonVersion = '1.3.0-SNAPSHOT'
version = '1.3.0'

def javaVersion = JavaVersion.VERSION_1_8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion


repositories {
    maven {
        url 'http://nexus.idorsia.com/repository/maven-central/'
        content {
            excludeGroup "com.idorsia.research"
            excludeGroup "com.actelion.research"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.actelion.research'
            artifactId 'lib-orbit-common'
            version version
            from components.java
//            artifact sourceJar {
//                classifier "sources"
//            }
//            artifact docJar {
//                classifier "javadoc"
//            }
            pom.withXml {
                asNode().appendNode('description', 'lib-orbit-common: Common objects for orbit image analysis.')
            }
        }
    }
    repositories {
        maven {
            credentials {
                username 'deployment'
                password 'biodeploy'
            }
            //url
            def releasesRepoUrl = "http://nexus.idorsia.com/repository/idorsia-releases"
            def snapshotsRepoUrl = "http://nexus.idorsia.com/repository/idorsia-snapshots"
            url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol true
        }
    }
}

signing {
    sign configurations.archives
}

if (javaVersion.isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

if(JavaVersion.current().isJava9Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addBooleanOption('html5', true)
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testCompile 'junit:junit:[4.0,)'
}

if (file('custom.gradle').exists()) {
    apply from: 'custom.gradle'
}


