/*
 *     Orbit, a versatile image analysis software for biological image-based quantification.
 *     Copyright (C) 2009 - 2017 Idorsia Pharmaceuticals Ltd., Hegenheimermattweg 91, CH-4123 Allschwil, Switzerland.
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

buildscript {
    repositories {
        maven {
            url 'https://nexus.idorsia.com/repository/maven-central/'
            content {
                excludeGroup "com.idorsia.research"
                excludeGroup "com.actelion.research"
            }
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.22.0"
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group 'com.actelion.research'

ext.moduleName = 'lib-orbit-common'

version = '1.3.1'
ext.libOrbitCommonVersion = version
//ext.libOrbitCommonVersion = '1.3.0-SNAPSHOT'

def javaVersion = JavaVersion.VERSION_1_8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:unchecked"
    }
}

repositories {
    maven {
        url 'https://nexus.idorsia.com/repository/maven-central/'
        content {
            excludeGroup "com.idorsia.research"
            excludeGroup "com.actelion.research"
        }
    }
    mavenLocal()
}

if (javaVersion.isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

if(JavaVersion.current().isJava9Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addBooleanOption('html5', true)
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation 'junit:junit:[4.0,)'
}

if (file('custom.gradle').exists()) {
    apply from: 'custom.gradle'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.actelion.research'
            artifactId 'lib-orbit-common'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username 'deployment'
                password 'biodeploy'
            }
            def releasesRepoUrl = "https://nexus.idorsia.com/repository/idorsia-releases"
            def snapshotsRepoUrl = "https://nexus.idorsia.com/repository/idorsia-snapshots"
            url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol true
        }
    }
}

